options {
    STATIC = false;
    UNICODE_INPUT = true;
    JAVA_UNICODE_ESCAPE = true;
}

PARSER_BEGIN(ExpressionsParser)

package logic.parser;

import logic.ast.logic.*;
import logic.ast.symbols.*;
import logic.ast.types.*;
import logic.ast.*;

import java.util.Stack;

public class ExpressionsParser {
}

PARSER_END(ExpressionsParser)

<PL, FOL, ND> SKIP : {
  < RESET: "\r">
  | < SPACE: " ">
  | < TAB: ("\t")>
  | < ENTER: "\n">
  | < SINGLE_LINE_COMMENT: "//" (~["\n", "\r"])* ("\n" | "\r" | "\r\n") >
  | < MULTI_LINE_COMMENT: "/*" (~["*"])* "*" (~["/"] (~["*"])* "*")* "/">
}

//Symbols
<PL, FOL, ND> TOKEN : {
    < LPAR : "(" >
  | < RPAR : ")" >
  | < TOP: "⊤" >
  | < BOTTOM: "⊥" >
  | < NOT: "¬" >
  | < AND: "∧" >
  | < OR: "∨" >
  | < CONDITIONAL: "→" >
  | < BICONDITIONAL: "↔" >
  | < GREEK : ("φ" | "δ" | "ψ" | "α" | "β" | "γ" | "ε" | "λ" | "μ" | "ρ" | "σ" | "τ" | "ω") >
  | < DOT : "." >
  | < LRPAR : "[" >
  | < RRPAR : "]" >
  | < LBRA : "{" >
  | < RBRA : "}" >
  | < COMMA : "," >
  | < NUMBER : (["0"-"9"])+ >
  | < PROVE: "prove">
  | < CONSEQUENCE: "⊢">
  | < COLON: ":">
}

<PL> TOKEN : {
  < LITERAL: ["a"-"z"] >
}

<FOL> TOKEN : {
  < VARIABLE: ["w"-"z"] (["0"-"9"])* >
  | < FUNCTION: ["a"-"v"] | ["a"-"z"](["a"-"z", "A"-"Z", "0"-"9"])+ >
  | < PREDICATE: ["A"-"Z"](["a"-"z", "A"-"Z", "0"-"9"])* >
  | < UNIVERSAL: "∀" >
  | < EXISTENTIAL: "∃" >
}

<ND> TOKEN : {
    < INEG: "¬I" >
  | < ENEG: "¬E" >
  | < ELAND: "∧EL" >
  | < ERAND: "∧ER" >
  | < IAND: "∧I" >
  | < ILOR: "∨IL" >
  | < IROR: "∨IR" >
  | < EOR: "∨E" >
  | < ICOND: "→I" >
  | < ECOND: "→E" >
  | < IUNI: "∀I" >
  | < EUNI: "∀E" >
  | < IEXIST: "∃I" >
  | < EEXIST: "∃E" >
  | < HYPOTHESIS: "H" >
  | < PREMISSE: "P" >
}

/*
* Parser for propositional logic expressions
*/

Exp parsePL() : {
    Exp e;
} {
    { token_source.curLexState = PL;}
    e = operations() (<DOT> | <EOF>) { return e; }
}

private Exp operations() : {
    Exp e1, e2;
} {
    e1 = atomicsPL()
    (
        <AND> e2 = atomicsPL() { e1 = new ASTAnd(e1, e2); }
      | <OR> e2 = atomicsPL() { e1 = new ASTOr(e1, e2); }
      | <CONDITIONAL> e2 = atomicsPL() { e1 = new ASTImplication(e1, e2); }
      | <BICONDITIONAL> e2 = atomicsPL() { e1 = new ASTBiconditional(e1, e2); }
    )?
    { return e1; }
}

private Exp atomicsPL() : {
    Token t;
    Exp e;
} {
    <TOP> { return new ASTBool(true); }
  | <BOTTOM> { return new ASTBool(false); }
  | <NOT> e = atomicsPL() { return new ASTNot(e); }
  | <LPAR> e = operations() <RPAR> { return new ASTParenthesis(e); }
  | t = <LITERAL> { return new ASTLiteral(t.image); }
  | t = <GREEK> { return new ASTLiteral(t.image); }
}

/*
* Parser for first-order logic expressions
*/

Exp parseFOL() : {
    Exp e;
} {
{ token_source.curLexState = FOL; }
    e = operationsFOL() (<DOT> | <EOF>)
    { return e; }
}

private Exp operationsFOL() : {
    Exp e1, e2;
} {
    e1 = atomicsFOL()
    (
        <AND> e2 = atomicsFOL() { e1 = new ASTAnd(e1, e2); }
      | <OR> e2 = atomicsFOL() { e1 = new ASTOr(e1, e2); }
      | <CONDITIONAL> e2 = atomicsFOL() { e1 = new ASTImplication(e1, e2); }
      | <BICONDITIONAL> e2 = atomicsFOL() { e1 = new ASTBiconditional(e1, e2); }
    )?
    { return e1; }
}

private Exp atomicsFOL() : {
    Token name, t;
    Exp e, e1;
    ASTPred pred;
} {
    <TOP> { return new ASTBool(true); }
  | <BOTTOM> { return new ASTBool(false); }
  | <NOT> e = atomicsFOL() { return new ASTNot(e); }
  | <LPAR> e = operationsFOL() <RPAR> { return new ASTParenthesis(e); }
  | t = <GREEK> { return new ASTLiteral(t.image); }
  | <EXISTENTIAL> t = <VARIABLE> {e = new ASTVariable(t.image);} e1 = atomicsFOL() { return new ASTExistential(e, e1); }
  | <UNIVERSAL> t = <VARIABLE> {e = new ASTVariable(t.image);} e1 = atomicsFOL() { return new ASTUniversal(e, e1); }
  | name = <PREDICATE> (e = predicateFOL(name.image) {return  e;} | {return new ASTLiteral(name.image); })
}

private Exp predicateFOL(String name) : {
    Exp e;
    ASTPred pred = new ASTPred(name);
} {
    <LPAR> e = termFOL() { pred.addTerm(e);} ( <COMMA> e = termFOL() { pred.addTerm(e); } )* <RPAR> {return pred; }
}

private Exp termFOL() : {
   Token t;
   Exp e;
   ASTFun fun;
} {
    t = <VARIABLE> { return new ASTVariable(t.image); }
  | t = <FUNCTION> (e = functionFOL(t.image) { return e; } | { return new ASTConstant(t.image); })
}

private Exp functionFOL(String name) : {
    Exp e;
    ASTFun fun = new ASTFun(name);
} {
    <LPAR> e = termFOL() { fun.addTerm(e); } ( <COMMA> e = termFOL() { fun.addTerm(e); } )* <RPAR> { return fun; }
}




